import { Docs } from "layouts/JsDocsLayout.bs.js";
import { make as ApiIntro } from "components/ApiIntro.bs.js";
export default Docs.make;

# String

<ApiIntro>

Provide bindings to JS string. Optimized for pipe-last.

</ApiIntro>

## t

```re sig
type t = string;
```

## make

```re sig
let make: 'a => t;
```

`make(value)` converts the given value to a `string`.

```re example
Js.log(Js.String2.make(3.5) == "3.5");
Js.log(Js.String2.make([|1,2,3|]) == "1,2,3");
```

## fromCharCode

```re sig
let fromCharCode: int => t;
```

`fromCharCode(n)` creates a `string` containing the character corresponding to that number; `n` ranges from 0 to 65535.
If out of range, the lower 16 bits of the value are used. Thus, `fromCharCode(0x1F63A)` gives the same result as `fromCharCode(0xF63A)`.

```re sig
Js.log(Js.String2.fromCharCode(65) == "A");
Js.log(Js.String2.fromCharCode(0x3c8) == {js|Ïˆ|js});
Js.log(Js.String2.fromCharCode(0xd55c) == {js|í•œ|js});
Js.log(Js.String2.fromCharCode(-64568) == {js|Ïˆ|js});
```

## fromCharCodeMany

```re sig
let fromCharCodeMany: array(int) => t;
```

`fromCharCodeMany([|n1;n2;n3|])` creates a `string` from the characters corresponding to the given numbers, using the same rules as `fromCharCode`.

## fromCodePoint

```re sig
let fromCodePoint: int => t;
```

`fromCodePoint(n)` creates a `string` containing the character corresponding to that numeric code point. 
If the number is not a valid code point, it raises `RangeError`. 
Thus, `fromCodePoint(0x1F63A)` will produce a correct value, unlike `fromCharCode(0x1F63A)`, and `fromCodePoint(-5)` will raise a `RangeError`.
 
```re example
Js.log(Js.String2.fromCodePoint(65) == "A");
Js.log(Js.String2.fromCodePoint(0x3c8) == {js|Ïˆ|js});
Js.log(Js.String2.fromCodePoint(0xd55c) == {js|í•œ|js});
Js.log(Js.String2.fromCodePoint(0x1f63a) == {js|ðŸ˜º|js});
```

## fromCodePointMany

```re sig
let fromCodePointMany: array(int) => t;
```

`fromCodePointMany([|n1;n2;n3|])` creates a `string` from the characters corresponding to the given code point numbers, using the same rules as `fromCodePoint`.

```re example
Js.log(Js.String2.fromCodePointMany([|0xd55c, 0xae00, 0x1f63a|]) == {js|í•œê¸€ðŸ˜º|js});
```

## length

```re sig
let length: t => int;
```

`length(s)` returns the length of the given `string`.

```re example
Js.log(Js.String2.length("abcd") == 4);
```

## get

```re sig
let get: (t, int) => t;
```

`get(s, n)` returns as a `string` the character at the given index number. 
If `n` is out of range, this function returns `undefined`,so at some point this function may be modified to return `option(string)`.

```re example
Js.log(Js.String2.get("Reason", 0) == "R");
Js.log(Js.String2.get("Reason", 4) == "o");
Js.log(Js.String2.get({js|Ráº½asÃ¶Å„|js}, 5) == {js|Å„|js});
```

## charAt

```re sig
let charAt: (int, t) => t;
```

`charAt(n, s)` gets the character at index `n` within string `s`. 
If `n` is negative or greater than the length of `s`, it returns the empty string.
If the string contains characters outside the range \u0000-\uffff, it will return the first 16-bit value at that position in the string.

```re example
Js.log(Js.String.charAt(0, "Reason") == "R");
Js.log(Js.String.charAt(12, "Reason") == "");
Js.log(Js.String.charAt(5, {js|Ráº½asÃ¶Å„|js}) == {js|Å„|js});
```

## charCodeAt

```re sig
let charCodeAt: (int, t) => float;
```

`charCodeAt(n, s)` returns the character code at position `n` in string `s`; the result is in the range 0-65535, unlke `codePointAt`, so it will not work correctly for characters with code points greater than or equal to 0x10000. 
The return type is `float` because this function returns NaN if `n` is less than zero or greater than the length of the string.

```re example
Js.log(Js.String.charCodeAt(0, {js|ðŸ˜º|js}) == 0xd83d->float_of_int);
Js.log(Js.String.codePointAt(0, {js|ðŸ˜º|js}) == Some(0x1f63a));
```

## codePointAt

```re sig
let codePointAt: (int, t) => option(int);
```

`codePointAt(n, s)` returns the code point at position `n` within string `s` as a `Some(value)`. 
The return value handles code points greater than or equal to 0x10000.
If there is no code point at the given position, the function returns `None`.

```re example
Js.log(Js.String.codePointAt(1, {js|Â¿ðŸ˜º?|js}) == Some(0x1f63a));
Js.log(Js.String.codePointAt(5, "abc") == None);
```

## concat

```re sig
let concat: (t, t) => t;
```

`concat(append, original)` returns a new `string` with `append` added after `original`.

```re example
Js.log(Js.String.concat("bell", "cow") == "cowbell");
```

## concatMany

```re sig
let concatMany: (array(t), t) => t;
```

`concat(arr, original)` returns a new `string` consisting of each item of an array of strings added to the `original` string.

```re example
Js.log(Js.String.concatMany([|"2nd", "3rd", "4th"|], "1st") == "1st2nd3rd4th");
```

## endsWith

```re sig
let endsWith: (t, t) => bool;
```

ES2015: `endsWith(substr, str)` returns `true` if the `str` ends with `substr`, `false` otherwise.

```re example
Js.log(Js.String.endsWith("Script", "BuckleScript") == true);
Js.log(Js.String.endsWith("Script", "BuckleShoes") == false);
```

## endsWithFrom

```re sig
let endsWithFrom: (t, int, t) => bool;
```

`endsWithFrom(ending, len, str)` returns `true` if the first len characters of `str` end with `ending`, `false` otherwise.
If `len` is greater than or equal to the length of `str`, then it works like `endsWith`. (Honestly, this should have been named endsWithAt, but oh well.)

```re example
Js.log(Js.String.endsWithFrom("cd", 4, "abcd") == true);
Js.log(Js.String.endsWithFrom("cd", 3, "abcde") == false);
Js.log(Js.String.endsWithFrom("cde", 99, "abcde",) == true);
Js.log(Js.String.endsWithFrom("ple", 7, "example.dat") == true);
```

## includes

```re sig
let includes: (t, t) => bool;
```

ES2015: `includes(searchValue, str)` returns `true` if `searchValue` is found anywhere within `str`, false otherwise.

```re example
Js.log(Js.String.includes("gram", "programmer") == true);
Js.log(Js.String.includes("er", "programmer") == true);
Js.log(Js.String.includes("pro", "programmer") == true);
Js.log(Js.String.includes("xyz", "programmer.dat") == false);
```

## includesFrom

```re sig
let includesFrom: (t, int, t) => bool;
```

ES2015: `includes(searchValue start, str)` returns `true` if `searchValue` is found anywhere within `str` starting at character number `start` (where 0 is the first character), `false` otherwise.

```re example
Js.log(Js.String.includesFrom("gram", 1, "programmer") == true);
Js.log(Js.String.includesFrom("gram", 4, "programmer") == false);
Js.log(Js.String.includesFrom({js|í•œ|js}, 1, {js|ëŒ€í•œë¯¼êµ­|js}) == true);
```

## indexOf

```re sig
let indexOf: (t, t) => int;
```

ES2015: `indexOf(searchValue, str)` returns the position at which `searchValue` was first found within `str`, or -1 if `searchValue` is not in `str`.

```re example
Js.log(Js.String.indexOf("ok", "bookseller") == 2);
Js.log(Js.String.indexOf("sell", "bookseller") == 4);
Js.log(Js.String.indexOf( "ee", "beekeeper") == 1);
Js.log(Js.String.indexOf("xyz", "bookseller") == -1);
```

## indexOfFrom

```re sig
let indexOfFrom: (t, t, int) => int;
```
`indexOfFrom(searchValue, start, str)` returns the position at which `searchValue` was found within `str` starting at character position `start`, or -1 if `searchValue` is not found in that portion of `str`.
The return value is relative to the beginning of the string, no matter where the search started from.

```re example
Js.log(Js.String.indexOfFrom("ok", 1, "bookseller") == 2);
Js.log(Js.String.indexOfFrom("sell", 2, "bookseller") == 4);
Js.log(Js.String.indexOfFrom("sell", 5, "bookseller") == -1);
```

## lastIndexOf

```re sig
let lastIndexOf: (t, t) => int;
```

`lastIndexOf(searchValue, str)` returns the position of the last occurrence of `searchValue` within `str`, searching backwards from the end of the string.
Returns -1 if `searchValue` is not in `str`. The return value is always relative to the beginning of the string.

```re example
Js.log(Js.String.lastIndexOf("ok", "bookseller") == 2);
Js.log(Js.String.lastIndexOf("ee", "beekeeper") == 4);
Js.log(Js.String.lastIndexOf("xyz", "abcdefg") == -1);
```

## lastIndexOfFrom

```re sig
let lastIndexOfFrom: (t, int, t) => int;
```

`lastIndexOfFrom(searchValue, start, str)` returns the position of the last occurrence of `searchValue` within `str`, searching backwards from the given start position.
Returns -1 if `searchValue` is not in `str`. The return value is always relative to the beginning of the string.

```re example
Js.log(Js.String.lastIndexOfFrom("ok", 6, "bookseller") == 2);
Js.log(Js.String.lastIndexOfFrom("ee", 8, "beekeeper") == 4);
Js.log(Js.String.lastIndexOfFrom("ee", 3, "beekeeper") == 1);
Js.log(Js.String.lastIndexOfFrom("xyz", 4, "abcdefg") == -1);
```

## localeCompare

```re sig
let localeCompare: (t, t) => float;
```

`localeCompare(comparison, reference)` returns
- a negative value if reference comes before comparison in sort order
- zero if reference and comparison have the same sort order
- a positive value if reference comes after comparison in sort order

```re example
Js.log(Js.String.localeCompare("ant", "zebra") > 0.0);
Js.log(Js.String.localeCompare("zebra", "ant") < 0.0);
Js.log(Js.String.localeCompare("cat", "cat") == 0.0);
Js.log(Js.String.localeCompare("cat", "CAT") > 0.0);
```

## match

```re sig
let match: (Js_re.t, t) => option(array(t));
```

`match(regexp, str)` matches a `string` against the given `regexp`. If there is no match, it returns `None`. For regular expressions without the g modifier, if there is a match, the return value is `Some(array)` where the array contains:
- The entire matched string
- Any capture groups if the regexp had parentheses
For regular expressions with the g modifier, a matched expression returns `Some(array)` with all the matched substrings and no capture groups.

```re example
Js.log(Js.String.match([%re "/b[aeiou]t/"], "The better bats") == Some([|"bet"|]));
Js.log(Js.String.match([%re "/b[aeiou]t/g"], "The better bats") == Some([|"bet", "bat"|]));
Js.log(Js.String.match([%re "/(\\d+)-(\\d+)-(\\d+)/"], "Today is 2018-04-05.") == Some([|"2018-04-05", "2018", "04", "05"|]));
Js.log(Js.String.match([%re "/b[aeiou]g/"], "The large container.") == None);
```

## normalize

```re sig
let normalize: t => t;
```

`normalize(str)` returns the normalized Unicode string using Normalization Form Canonical (NFC) Composition.
Consider the character Ã£, which can be represented as the single codepoint \u00e3 or the combination of a lower case letter A \u0061 and a combining tilde \u0303.
Normalization ensures that both can be stored in an equivalent binary representation.
See also Unicode technical report for details.

## normalizeByForm

```re sig
let normalizeByForm: (t, t) => t;
```

ES2015: `normalize(form, str)` returns the normalized Unicode string using the specified form of normalization, which may be one of:
- "NFC" â€” Normalization Form Canonical Composition.
- "NFD" â€” Normalization Form Canonical Decomposition.
- "NFKC" â€” Normalization Form Compatibility Composition.
- "NFKD" â€” Normalization Form Compatibility Decomposition.

See also Unicode technical report for details.

## repeat

```re sig
let repeat: (t, int) => t;
```

`repeat(n, str)` returns a `string` that consists of `n` repetitions of `str`. Raises `RangeError` if `n` is negative.

```re example
Js.log(Js.String.repeat(3, "ha") == "hahaha");
Js.log(Js.String.repeat(0, "empty") == "");
```

## replace

```re sig
let replace: (t, t, t) => t;
```

ES2015: `replace(substr, newSubstr, str)` returns a new `string` which is identical to `str` except with the first matching instance of `substr` replaced by `newSubstr`.
`substr` is treated as a verbatim string to match, not a regular expression.

```re example
Js.log(Js.String.replace("old", "new", "old string") == "new string");
Js.log(Js.String.replace("the", "this", "the cat and the dog") == "this cat and the dog");
```

## replaceByRe

```re sig
let replaceByRe: (Js_re.t, t, t) => t;
```

`replaceByRe(regex, replacement, str)` returns a new `string` where occurrences matching regex have been replaced by `replacement`.

```re example
Js.log(Js.String.replaceByRe([%re "/[aeiou]/g"], "x", "vowels be gone") == "vxwxls bx gxnx");
Js.log(Js.String.replaceByRe([%re "/(\\w+) (\\w+)/"], "$2, $1", "Juan Fulano") == "Fulano, Juan");
```

## unsafeReplaceBy0

```re sig
let unsafeReplaceBy0: (Js_re.t, (t, int, t) => t, t) => t;
```

Returns a new `string` with some or all matches of a pattern with no capturing parentheses replaced by the value returned from the given function.
The function receives as its parameters the matched string, the offset at which the match begins, and the whole string being matched.

```re example
let str = "beautiful vowels";
let re = [%re "/[aeiou]/g"];
let matchFn = (matchPart, offset, wholeString) =>
  Js.String.toUpperCase(matchPart);

let replaced = Js.String.unsafeReplaceBy0(re, matchFn, str);

Js.log(replaced); /* prints "bEAUtifUl vOwEls" */
```

## unsafeReplaceBy1

```re sig
let unsafeReplaceBy1: (Js_re.t, (t, t, int, t) => t, t) => t;
```

Returns a new `string` with some or all matches of a pattern with one set of capturing parentheses replaced by the value returned from the given function.
The function receives as its parameters the matched string, the captured string, the offset at which the match begins, and the whole string being matched.

```re example
let str = "increment 23";
let re = [%re "/increment (\\d+)/g"];
let matchFn = (matchPart, p1, offset, wholeString) =>
  wholeString ++ " is " ++ string_of_int(int_of_string(p1) + 1);

let replaced = Js.String.unsafeReplaceBy1(re, matchFn, str);

Js.log(replaced); /* prints "increment 23 is 24" */
```

## unsafeReplaceBy2

```re sig
let unsafeReplaceBy2: (Js_re.t, (t, t, t, int, t) => t, t) => t;
```

Returns a new `string` with some or all matches of a pattern with two sets of capturing parentheses replaced by the value returned from the given function.
The function receives as its parameters the matched string, the captured strings, the offset at which the match begins, and the whole string being matched.

```re example
let str = "7 times 6";
let re = [%re "/(\\d+) times (\\d+)/"];
let matchFn = (matchPart, p1, p2, offset, wholeString) =>
  string_of_int(int_of_string(p1) * int_of_string(p2));

let replaced = Js.String.unsafeReplaceBy2(re, matchFn, str);

Js.log(replaced); /* prints "42" */
```

## unsafeReplaceBy3

```re sig
let unsafeReplaceBy3: (Js_re.t, (t, t, t, t, int, t) => t, t) => t;
```

Returns a new `string` with some or all matches of a pattern with three sets of capturing parentheses replaced by the value returned from the given function.
The function receives as its parameters the matched string, the captured strings, the offset at which the match begins, and the whole string being matched.

## search

```re sig
let search: (Js_re.t, t) => int;
```

`search(regexp, str)` returns the starting position of the first match of `regexp` in the given `str`, or -1 if there is no match.

```re example
Js.log(Js.String.search([%re "/\\d+/"], "testing 1 2 3") == 8);
Js.log(Js.String.search([%re "/\\d+/"], "no numbers") == -1);
```

## slice

```re sig
let slice: (~from: int, ~to_: int, t) => t;
```

`slice(from:n1, to_:n2, str)` returns the substring of `str` starting at character `n1` up to but not including `n2`.
- If either `n1` or `n2` is negative, then it is evaluated as `length(str - n1)` or `length(str - n2)`.
- If `n2` is greater than the length of `str`, then it is treated as `length(str)`.
- If `n1` is greater than `n2`, slice returns the empty string.

```re example
Js.log(Js.String.slice(~from=2, ~to_=5, "abcdefg") == "cde");
Js.log(Js.String.slice(~from=2, ~to_=9, "abcdefg") == "cdefg");
Js.log(Js.String.slice(~from=(-4), ~to_=(-2), "abcdefg") == "de");
Js.log(Js.String.slice(~from=5, ~to_=1, "abcdefg") == "");
```

## sliceToEnd

```re sig
let sliceToEnd: (~from: int, t) => t;
```

`sliceToEnd(str, from:n)` returns the substring of `str` starting at character `n` to the end of the string.
- If `n` is negative, then it is evaluated as `length(str - n)`.
- If `n` is greater than the length of `str`, then sliceToEnd returns the empty string.

```re example
Js.log(Js.String.sliceToEnd(~from=4, "abcdefg") == "efg");
Js.log(Js.String.sliceToEnd(~from=(-2), "abcdefg") == "fg");
Js.log(Js.String.sliceToEnd(~from=7, "abcdefg") == "");
```

## split

```re sig
let split: (t, t) => array(t);
```

`split(delimiter, str)` splits the given `str` at every occurrence of `delimiter` and returns an array of the resulting substrings.

```re example
Js.log(Js.String.split("-", "2018-01-02") == [|"2018", "01", "02"|]);
Js.log(Js.String.split(",", "a,b,,c") == [|"a", "b", "", "c"|]);
Js.log(Js.String.split("::", "good::bad as great::awful") == [|"good", "bad as great", "awful"|]);
Js.log(Js.String.split(";", "has-no-delimiter") == [|"has-no-delimiter"|]);
```

## splitAtMost

```re sig
let splitAtMost: (t, ~limit: int, t) => array(t);
```

`splitAtMost(delimiter, ~limit:n, str)` splits the given `str` at every occurrence of `delimiter` and returns an array of the first `n` resulting substrings.
If `n` is negative or greater than the number of substrings, the array will contain all the substrings.

```re example
Js.log(Js.String.splitAtMost("/", ~limit=3, "ant/bee/cat/dog/elk") == [|"ant", "bee", "cat"|]);
Js.log(Js.String.splitAtMost("/", ~limit=0, "ant/bee/cat/dog/elk") == [||]);
Js.log(Js.String.splitAtMost("/", ~limit=9, "ant/bee/cat/dog/elk") == [|"ant", "bee", "cat", "dog", "elk"|]);
```

## splitLimited

```re sig
let splitLimited: (t, int, t) => array(t);
```

Deprecated - Please use splitAtMost.

## splitByRe

```re sig
let splitByRe: (Js_re.t, t) => array(option(t));
```

`splitByRe(regex, str)` splits the given `str` at every occurrence of `regex` and returns an array of the resulting substrings.

```re example
Js.log(
  Js.String.splitByRe([%re "/\\s*[,;]\\s*/"], "art; bed , cog ;dad")
  == [|Some("art"), Some("bed"), Some("cog"), Some("dad")|],
);
```

## splitByReAtMost

```re sig
let splitByReAtMost: (Js_re.t, ~limit: int, t) => array(option(t));
```

`splitByReAtMost(regex, ~limit:n, str)` splits the given `str` at every occurrence of `regex` and returns an array of the first `n` resulting substrings.
If `n` is negative or greater than the number of substrings, the array will contain all the substrings.

```re example
Js.log(
  Js.String.splitByReAtMost([%re "/\\s*:\\s*/"], ~limit=3, "one: two: three: four")
  == [|Some("one"), Some("two"), Some("three")|],
);
Js.log(
  Js.String.splitByReAtMost([%re "/\\s*:\\s*/"], ~limit=0, "one: two: three: four")
  == [||],
);
Js.log(
  Js.String.splitByReAtMost([%re "/\\s*:\\s*/"], ~limit=8, "one: two: three: four")
  == [|Some("one"), Some("two"), Some("three"), Some("four")|],
);
```

## splitRegexpLimited

```re sig
let splitRegexpLimited: (Js_re.t, int, t) => array(t);
```

Deprecated - Please use splitByReAtMost.

## startsWith

```re sig
let startsWith: (t, t) => bool;
```

ES2015: `startsWith(substr, str)` returns `true` if the `str` starts with `substr`, `false` otherwise.

```re example
Js.log(Js.String.startsWith("Buckle", "BuckleScript") == true);
Js.log(Js.String.startsWith("", "BuckleScript") == true);
Js.log(Js.String.startsWith("Buckle", "JavaScript") == false);
```

## startsWithFrom

```re sig
let startsWithFrom: (t, int, t) => bool;
```

ES2015: `startsWithFrom(substr, n, str)` returns `true` if the `str` starts with `substr` starting at position `n`, false otherwise.
If `n` is negative, the search starts at the beginning of `str`.

```re example
Js.log(Js.String.startsWithFrom("kle", 3, "BuckleScript") == true);
Js.log(Js.String.startsWithFrom("", 3, "BuckleScript") == true);
Js.log(Js.String.startsWithFrom("Buckle", 2, "JavaScript") == false);
```

## substr

```re sig
let substr: (~from: int, t) => t;
```

`substr(~from:n, str)` returns the substring of `str` from position `n` to the end of the string.
- If `n` is less than zero, the starting position is the length of `str - n`.
- If `n` is greater than or equal to the length of `str`, returns the empty string.

```re example
Js.log(Js.String.substr(~from=3, "abcdefghij") == "defghij");
Js.log(Js.String.substr(~from=(-3), "abcdefghij") == "hij");
Js.log(Js.String.substr(~from=12, "abcdefghij") == "");
```

## substrAtMost

```re sig
let substrAtMost: (~from: int, ~length: int, t) => t;
```

`substrAtMost(~from: pos, ~length: n, str)` returns the substring of `str` of length `n` starting at position `pos`.
- If `pos` is less than zero, the starting position is the length of `str - pos`.
- If `pos` is greater than or equal to the length of `str`, returns the empty string.
- If `n` is less than or equal to zero, returns the empty string.

```re example
Js.log(Js.String.substrAtMost(~from=3, ~length=4, "abcdefghij") == "defg");
Js.log(Js.String.substrAtMost(~from=(-3), ~length=4, "abcdefghij") == "hij");
Js.log(Js.String.substrAtMost(~from=12, ~length=2, "abcdefghij") == "");
```

## substring

```re sig
let substring: (~from: int, ~to_: int, t) => t;
```

`substring(~from: start, ~to_: finish, str)` returns characters `start` up to but not including finish from `str`.
- If `start` is less than zero, it is treated as zero.
- If `finish` is zero or negative, the empty string is returned.
- If `start` is greater than `finish`, the `start` and `finish` points are swapped.

```re example
Js.log(Js.String.substring(~from=3, ~to_=6, "playground") == "ygr");
Js.log(Js.String.substring(~from=6, ~to_=3, "playground") == "ygr");
Js.log(Js.String.substring(~from=4, ~to_=12, "playground") == "ground");
```

## substringToEnd

```re sig
let substringToEnd: (~from: int, t) => t;
```

`substringToEnd(~from: start, str)` returns the substring of `str` from position `start` to the end.
- If `start` is less than or equal to zero, the entire string is returned.
- If `start` is greater than or equal to the length of `str`, the empty string is returned.

```re example
Js.log(Js.String.substringToEnd(~from=4, "playground") == "ground");
Js.log(Js.String.substringToEnd(~from=(-3), "playground") == "playground");
Js.log(Js.String.substringToEnd(~from=12, "playground") == "");
```

## toLowerCase

```re sig
let toLowerCase: t => t;
```

`toLowerCase(str)` converts `str` to lower case using the locale-insensitive case mappings in the Unicode Character Database.
Notice that the conversion can give different results depending upon context, for example with the Greek letter sigma, which has two different lower case forms when it is the last character in a string or not.

```re example
Js.log(Js.String.toLowerCase("ABC") == "abc");
Js.log(Js.String.toLowerCase({js|Î£Î |js}) == {js|ÏƒÏ€|js});
Js.log(Js.String.toLowerCase({js|Î Î£|js}) == {js|Ï€Ï‚|js});
```

## toLocaleLowerCase

```re sig
let toLocaleLowerCase: t => t;
```

`toLocaleLowerCase(str)` converts `str` to lower case using the current locale.

## toUpperCase

```re sig
let toUpperCase: t => t;
```

`toUpperCase(str)` converts `str` to upper case using the locale-insensitive case mappings in the Unicode Character Database. 
Notice that the conversion can expand the number of letters in the result; for example the German ÃŸ capitalizes to two Ses in a row.

```re example
Js.log(Js.String.toUpperCase("abc") == "ABC");
Js.log(Js.String.toUpperCase({js|StraÃŸe|js}) == {js|STRASSE|js});
Js.log(Js.String.toUpperCase({js|Ï€Ï‚|js}) == {js|Î Î£|js});
```

## toLocaleUpperCase

```re sig
let toLocaleUpperCase: t => t;
```

`toLocaleUpperCase(str)` converts `str` to upper case using the current locale.

## trim

```re sig
let trim: t => t;
```

`trim(str)` returns a string that is `str` with whitespace stripped from both ends. Internal whitespace is not removed.

```re example
Js.log(Js.String.trim("   abc def   ") == "abc def");
Js.log(Js.String.trim("\n\r\t abc def \n\n\t\r ") == "abc def");
```

## anchor

```re sig
let anchor: (t, t) => t;
```

`anchor(anchorName, anchorText)` creates a string with an HTML `<a>` element with name attribute of `anchorName` and `anchorText` as its content.

```re example
Js.log(Js.String.anchor("page1", "Page One")); /* prints "<a name=\"page1\">Page One</a>" */
```

## link

```re sig
let link: (t, t) => t;
```

ES2015: `link(urlText, linkText)` creates a string with an HTML `<a>` element with href attribute of `urlText` and `linkText` as its content.

```re example
Js.log(Js.String.link("page2.html", "Go to page two")); /* prints "<a href=\"page2.html\">Go to page two</a>" */
```

## castToArrayLike

```re sig
let castToArrayLike: t => Js_array2.array_like(t);
```

ES2015
