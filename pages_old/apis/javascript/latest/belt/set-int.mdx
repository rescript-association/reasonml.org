# SetInt

<Intro>

Specalized when value type is `int`, more efficient than the generic type, its compare behavior is fixed using the built-in comparison.

</Intro>

## value

```re sig
type value = int;
```

The type of the set elements.

## t

```re sig
type t;
```

Type of the sets.

## empty

```re sig
let empty: t;
```

Empty set

```re example
let s0 = Belt.Set.Int.empty;
```

## fromArray

```re sig
let fromArray: array(value) => t;
```

Creates new set from array of elements.

```re example
let s0 = Belt.Set.Int.fromArray([|1, 3, 2, 4|])

s0->Belt.Set.Int.toArray; /* [|1, 2, 3, 4|] */
```

## fromSortedArrayUnsafe

```re sig
let fromSortedArrayUnsafe: array(value) => t;
```

The same as [fromArray][#fromarray] except it is after assuming the input array is already sorted.

## isEmpty

```re sig
let isEmpty: t => bool;
```

Checks if set is empty.

```re example
let empty = Belt.Set.Int.fromArray([||]);
let notEmpty = Belt.Set.Int.fromArray([|1|]);

Belt.Set.Int.isEmpty(empty); /* true */
Belt.Set.Int.isEmpty(notEmpty); /* false */
```

## has

```re sig
let has: (t, value) => bool;
```

Checks if element exists in set.

```re example
let set = Belt.Set.Int.fromArray([|1, 4, 2, 5|]);

set->Belt.Set.Int.has(3) /* false */
set->Belt.Set.Int.has(1) /* true */
```

## add

```re sig
let add: (t, value) => t;
```

Adds element to set. If element existed in set, value is unchanged.

```re example
let s0 = Belt.Set.Int.empty;
let s1 = s0->Belt.Set.Int.add(1);
let s2 = s1->Belt.Set.Int.add(2);
let s3 = s2->Belt.Set.Int.add(2);
s0->Belt.Set.Int.toArray; /* [||] */
s1->Belt.Set.Int.toArray; /* [|1|] */
s2->Belt.Set.Int.toArray; /* [|1, 2|] */
s3->Belt.Set.Int.toArray; /* [|1,2 |] */
s2 == s3; /* true */
```

## mergeMany

```re sig
let mergeMany: (t, array(value)) => t;
```

Adds each element of array to set. Unlike [add](#add), the reference of return value might be changed even if all values in array already exist in set

```re example
let set = Belt.Set.Int.empty;

let newSet = set->Belt.Set.Int.mergeMany([|5, 4, 3, 2, 1|]);
newSet->Belt.Set.Int.toArray; /* [|1, 2, 3, 4, 5|] */
```

## remove

```re sig
let remove: (t, value) => t;
```

Removes element from set. If element wasn't existed in set, value is unchanged.

```re example
let s0 = Belt.Set.Int.fromArray([|2,3,1,4,5|]);
let s1 = s0->Belt.Set.Int.remove(1);
let s2 = s1->Belt.Set.Int.remove(3);
let s3 = s2->Belt.Set.Int.remove(3);

s1->Belt.Set.Int.toArray; /* [|2,3,4,5|] */
s2->Belt.Set.Int.toArray; /* [|2,4,5|] */
s2 == s3; /* true */
```

## removeMany

```re sig
let removeMany: (t, array(value)) => t;
```

Removes each element of array from set. Unlike [remove](#remove), the reference of return value might be changed even if any values in array not existed in set.

```re example
let set = Belt.Set.Int.fromArray([|1, 2, 3, 4|]);

let newSet = set->Belt.Set.Int.removeMany([|5, 4, 3, 2, 1|]);
newSet->Belt.Set.Int.toArray; /* [||] */
```

## union

```re sig
let union: (t, t) => t;
```

Returns union of two sets.

```re example
let s0 = Belt.Set.Int.fromArray([|5,2,3,5,6|]);
let s1 = Belt.Set.Int.fromArray([|5,2,3,1,5,4|]);
let union = Belt.Set.Int.union(s0, s1);
union->Belt.Set.Int.toArray; /* [|1,2,3,4,5,6|] */
```

## intersect

```re sig
let intersect: (t, t) => t;
```

Returns intersection of two sets.

```re example

let s0 = Belt.Set.Int.fromArray([|5,2,3,5,6|]);
let s1 = Belt.Set.Int.fromArray([|5,2,3,1,5,4|]);
let intersect = Belt.Set.Int.intersect(s0, s1);
intersect->Belt.Set.Int.toArray; /* [|2,3,5|] */
```

## diff

```re sig
let diff: (t, t) => t;
```

Returns elements from first set, not existing in second set.

```re example
let s0 = Belt.Set.Int.fromArray([|5,2,3,5,6|]);
let s1 = Belt.Set.Int.fromArray([|5,2,3,1,5,4|]);
Belt.Set.Int.toArray(Belt.Set.Int.diff(s0, s1)); /* [|6|] */
Belt.Set.Int.toArray(Belt.Set.Int.diff(s1,s0)); /* [|1,4|] */
```

## subset

```re sig
let subset: (t, t) => bool;
```

Checks if second set is subset of first set.

```re example
let s0 = Belt.Set.Int.fromArray([|5,2,3,5,6|]);
let s1 = Belt.Set.Int.fromArray([|5,2,3,1,5,4|]);
let s2 = Belt.Set.Int.intersect(s0, s1);
Belt.Set.Int.subset(s2, s0); /* true */
Belt.Set.Int.subset(s2, s1); /* true */
Belt.Set.Int.subset(s1, s0); /* false */
```

## cmp

```re sig
let cmp: (t, t) => int;
```

Total ordering between sets. Can be used as the ordering function for doing sets of sets. It compares size first and then iterates over each element following the order of elements.

## eq

```re sig
let eq: (t, t) => bool;
```

Checks if two sets are equal.

```re example
let s0 = Belt.Set.Int.fromArray([|5,2,3|]);
let s1 = Belt.Set.Int.fromArray([|3,2,5|]);

Belt.Set.Int.eq(s0, s1); /* true */
```

## forEachU

```re sig
let forEachU: (t, [@bs] (value => unit)) => unit;
```

Same as [forEach](##forEach) but takes uncurried functon.

## forEach

```re sig
let forEach: (t, value => unit) => unit;
```

Applies function `f` in turn to all elements of set in increasing order.

```re example
let s0 = Belt.Set.Int.fromArray([|5,2,3,5,6|]);
let acc = ref([]);
s0->Belt.Set.Int.forEach(x => {
  acc := Belt.List.add(acc^, x)
});
acc; /* [6,5,3,2] */
```

## reduceU

```re sig
let reduceU: (t, 'a, [@bs] (('a, value) => 'a)) => 'a;
```

## reduce

```re sig
let reduce: (t, 'a, ('a, value) => 'a) => 'a;
```

Applies function `f` to each element of set in increasing order. Function `f` has two parameters: the item from the set and an “accumulator”, which starts with a value of `initialValue`. `reduce` returns the final value of the accumulator.

```re example
let s0 = Belt.Set.Int.fromArray([|5,2,3,5,6|]);
s0->Belt.Set.Int.reduce([], (acc, element) =>
  acc->Belt.List.add(element)
); /* [6,5,3,2] */
```

## everyU

```re sig
let everyU: (t, [@bs] (value => bool)) => bool;
```

## every

```re sig
let every: (t, value => bool) => bool;
```

Checks if all elements of the set satisfy the predicate. Order unspecified.

```re example
let isEven = x => x mod 2 == 0;

let s0 = Belt.Set.Int.fromArray([|2,4,6,8|]);
s0->Belt.Set.Int.every(isEven); /* true */
```

## someU

```re sig
let someU: (t, [@bs] (value => bool)) => bool;
```

## some

```re sig
let some: (t, value => bool) => bool;
```

Checks if at least one element of the set satisfies the predicate.

```re example
let isOdd = x => x mod 2 != 0;

let s0 = Belt.Set.Int.fromArray([|1,2,4,6,8|]);
s0->Belt.Set.Int.some(isOdd); /* true */
```

## keepU

```re sig
let keepU: (t, [@bs] (value => bool)) => t;
```

## keep

```re sig
let keep: (t, value => bool) => t;
```

Returns the set of all elements that satisfy the predicate.

```re example
let isEven = x => x mod 2 == 0;

let s0 = Belt.Set.Int.fromArray([|1,2,3,4,5|]);
let s1 = s0->Belt.Set.Int.keep(isEven);

s1->Belt.Set.Int.toArray; /* [|2,4|] */
```

## partitionU

```re sig
let partitionU: (t, [@bs] (value => bool)) => (t, t);
```

## partition

```re sig
let partition: (t, value => bool) => (t, t);
```

Returns a pair of sets, where first is the set of all the elements of set that satisfy the predicate, and second is the set of all the elements of set that do not satisfy the predicate.

```re example
let isOdd = x => x mod 2 != 0;

let s0 = Belt.Set.Int.fromArray([|1,2,3,4,5|]);
let (s1, s2) = s0->Belt.Set.Int.partition(isOdd);

s1->Belt.Set.Int.toArray; /* [|1,3,5|] */
s2->Belt.Set.Int.toArray; /* [|2,4|] */
```

## size

```re sig
let size: t => int;
```

Returns size of the set.

```re example
let s0 = Belt.Set.Int.fromArray([|1,2,3,4|]);

s0->Belt.Set.Int.size; /* 4 */
```

## toList

```re sig
let toList: t => list(value);
```

Returns list of ordered set elements.

```re example
let s0 = Belt.Set.Int.fromArray([|3,2,1,5|]);

s0->Belt.Set.Int.toList; /* [1,2,3,5] */
```

## toArray

```re sig
let toArray: t => array(value);
```

Returns array of ordered set elements.

```re example
let s0 = Belt.Set.Int.fromArray([|3,2,1,5|]);

s0->Belt.Set.Int.toArray; /* [|1,2,3,5|] */
```

## minimum

```re sig
let minimum: t => option(value);
```

Returns minimum value of the collection. `None` if collection is empty.

```re example
let s0 = Belt.Set.Int.empty;
let s1 = Belt.Set.Int.fromArray([|3,2,1,5|]);

s0->Belt.Set.Int.minimum; /* None */
s1->Belt.Set.Int.minimum; /* Some(1) */
```

## minUndefined

```re sig
let minUndefined: t => Js.undefined(value);
```

Returns minimum value of the collection. `undefined` if collection is empty.

```re example
let s0 = Belt.Set.Int.empty;
let s1 = Belt.Set.Int.fromArray([|3,2,1,5|]);

s0->Belt.Set.Int.minUndefined; /* undefined */
s1->Belt.Set.Int.minUndefined; /* 1 */
```

## maximum

```re sig
let maximum: t => option(value);
```

Returns maximum value of the collection. `None` if collection is empty.

```re example
let s0 = Belt.Set.Int.empty;
let s1 = Belt.Set.Int.fromArray([|3,2,1,5|]);

s0->Belt.Set.Int.maximum; /* None */
s1->Belt.Set.Int.maximum; /* Some(5) */
```

## maxUndefined

```re sig
let maxUndefined: t => Js.undefined(value);
```

Returns maximum value of the collection. `undefined` if collection is empty.

```re example
let s0 = Belt.Set.Int.empty;
let s1 = Belt.Set.Int.fromArray([|3,2,1,5|]);

s0->Belt.Set.Int.maxUndefined; /* undefined */
s1->Belt.Set.Int.maxUndefined; /* 5 */
```

## get

```re sig
let get: (t, value) => option(value);
```

Returns the reference of the value which is equivalent to value using the comparator specifiecd by this collection. Returns `None` if element does not exist.

```re example
let s0 = Belt.Set.Int.fromArray([|1,2,3,4,5|]);

s0->Belt.Set.Int.get(3); /* Some(3) */
s0->Belt.Set.Int.get(20); /* None */
```

## getUndefined

```re sig
let getUndefined: (t, value) => Js.undefined(value);
```

Same as [get](#get) but returns `undefined` when element does not exist.

## getExn

```re sig
let getExn: (t, value) => value;
```

Same as [get](#get) but raise when element does not exist.

## split

```re sig
let split: (t, value) => ((t, t), bool);
```

Returns a tuple `((l, r), present)`, where `l` is the set of elements of set that are strictly less than value, `r` is the set of elements of set that are strictly greater than value, `present` is `false` if set contains no element equal to value, or `true` if set contains an element equal to value.

```re example
let s0 = Belt.Set.Int.fromArray([|1,2,3,4,5|]);

let ((smaller, larger), present) = s0->Belt.Set.Int.split(3);

present; /* true */
smaller->Belt.Set.Int.toArray; /* [|1,2|] */
larger->Belt.Set.Int.toArray; /* [|4,5|] */
```

## checkInvariantInternal

```re sig
let checkInvariantInternal: t => unit;
```

**raise** when invariant is not held
